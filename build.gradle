import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

def CLOUD_PUBSUB_VERSION = '0.43.0-beta'

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.cloud:google-cloud-pubsub:${CLOUD_PUBSUB_VERSION}"

    compile "com.fasterxml.jackson.core:jackson-core:2.1.3"
    compile "com.google.api:api-common:1.5.0"
    compile "com.google.api:gax:1.23.0"
    compile "com.google.api:gax-grpc:1.23.0"
    compile "com.google.api.grpc:grpc-google-cloud-pubsub-v1:0.8.0"
    compile "com.google.api.grpc:proto-google-cloud-pubsub-v1:0.8.0"
    compile "com.google.api.grpc:proto-google-common-protos:1.7.0"
    compile "com.google.api.grpc:proto-google-iam-v1:0.8.0"
    compile "com.google.auth:google-auth-library-credentials:0.9.0"
    compile "com.google.auth:google-auth-library-oauth2-http:0.9.0"
    compile "com.google.auto.value:auto-value:1.4"
    compile "com.google.cloud:google-cloud-core:1.25.0"
    compile "com.google.cloud:google-cloud-core-grpc:1.25.0"
    compile "com.google.code.findbugs:jsr305:3.0.1"
    compile "com.google.code.gson:gson:2.7"
    compile "com.google.errorprone:error_prone_annotations:2.1.2"
    compile "com.google.guava:guava:20.0"
    compile "com.google.http-client:google-http-client:1.23.0"
    compile "com.google.http-client:google-http-client-jackson2:1.19.0"
    compile "com.google.protobuf:protobuf-java:3.5.1"
    compile "com.google.protobuf:protobuf-java-util:3.5.1"
    compile "commons-codec:commons-codec:1.3"
    compile "commons-logging:commons-logging:1.1.1"
    compile "io.grpc:grpc-auth:1.10.1"
    compile "io.grpc:grpc-context:1.10.1"
    compile "io.grpc:grpc-core:1.10.1"
    compile "io.grpc:grpc-netty-shaded:1.10.1"
    compile "io.grpc:grpc-protobuf:1.10.1"
    compile "io.grpc:grpc-protobuf-lite:1.10.1"
    compile "io.grpc:grpc-stub:1.10.1"
    compile "io.opencensus:opencensus-api:0.11.0"
    compile "io.opencensus:opencensus-contrib-grpc-metrics:0.11.0"
    compile "joda-time:joda-time:2.9.2"
    compile "org.apache.httpcomponents:httpclient:4.0.1"
    compile "org.apache.httpcomponents:httpcore:4.0.1"
    compile "org.threeten:threetenbp:1.3.3"
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-output-google_pubsub_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtime.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtime.allDependencies.each { dep ->
            File f = configurations.runtime.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
    }
}

vendor.dependsOn(generateGemJarRequiresFile)
